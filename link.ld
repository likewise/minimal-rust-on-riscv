/* Copyright lowRISC contributors.
   Licensed under the Apache License, Version 2.0, see LICENSE for details.
   SPDX-License-Identifier: Apache-2.0 */

OUTPUT_ARCH(riscv)

MEMORY
{
    /* 992 kB should be enough for anybody... */
    ram         : ORIGIN = 0x00100000, LENGTH = 0xF000 /* 56 kB */
    stack       : ORIGIN = 0x0010F000, LENGTH = 0x1000  /* 4 kB */
}

/* Stack information variables */
_min_stack      = 0x800;   /* 4K - minimum stack space to reserve */
_stack_len     = LENGTH(stack);
_stack_bottom  = ORIGIN(stack);
_stack_top     = ORIGIN(stack) + LENGTH(stack);

_entry_point = _vectors_start + 0x80;
ENTRY(_entry_point)

/* The tohost address is used by Spike for a magic "stop me now" message. This
   is set to equal SIM_CTRL_CTRL (see simple_system_regs.h), which has that
   effect in simple_system simulations. Note that it must be 8-byte aligned.

   We don't read data back from Spike, so fromhost is set to some dummy value:
   we place it just above the top of the stack.
 */
tohost   = 0x20008;
fromhost = _stack_top + 0x10;

SECTIONS
{
    .vectors :
    {
        . = ALIGN(4);
        _vectors_start = .;
        KEEP(*(.vectors))
        _vectors_end = .;
    } > ram

    .text : {
        . =  _vectors_start + 0x80;
        _stext = .;
        KEEP(*(.riscv.start));
        *(.text)
        *(.text.*)
    }  > ram

    .rodata : {
        . = ALIGN(4);
        /* Small RO data before large RO data */
        *(.srodata)
        *(.srodata.*)
        *(.rodata)
        *(.rodata.*)
    } > ram

    . = ALIGN(4);
    _etext = .;

    .data : AT (_etext) {
        . = ALIGN(4);
        _sdata = .;

        PROVIDE(__global_pointer$ = . + 0x800);

        /* Small data before large data */
        *(.ramfunc .ramfunc.*)
        *(.sdata .sdata.* .gnu.linkonce.r.*)
        *(.data .data.*)
        . = ALIGN(4);
        _edata = .;
    } > ram

    .bss (NOLOAD) :
    {
        . = ALIGN(4);
        _bss_start = .;
        /* Small BSS before large BSS */
        *(.sbss)
        *(.sbss.*)
        *(.bss)
        *(.bss.*)
        *(COMMON)
        . = ALIGN(4);
        _bss_end = .;
    } > ram

    /* ensure there is enough room for stack */
    .stack (NOLOAD): {
        . = ALIGN(4);
        . = . + _min_stack ;
        . = ALIGN(4);
        stack = . ;
        _stack = . ;
    } > stack

    /* Discard RISC-V relevant .eh_frame, we are not doing unwind on panic
    so it is not needed. */
    /DISCARD/ :
    {
      *(.eh_frame);
      *(.ARM.exidx .ARM.exidx.*);
    }
}
